plugins {
	id 'java'
	id 'jacoco-report-aggregation'
}

allprojects {

    wrapper {
        gradleVersion = '8.14.2'
    }
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'checkstyle'
	apply plugin: 'jacoco'

	dependencies {
		testImplementation platform('org.junit:junit-bom:5.13.1')
		testImplementation 'org.junit.jupiter:junit-jupiter'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
		testImplementation 'org.mockito:mockito-core:5.15.2'
	}

	compileJava {
		options.release = 21
		options.debug = true
		options.debugOptions.debugLevel = 'source,vars,lines'
		options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
	}

    test {
        useJUnitPlatform()
    }

	jacocoTestReport {
		reports {
			xml.required = true // codecov depends on xml format report
			html.required = true
		}
	}

	checkstyle {
		toolVersion = '10.26.0'
		configDirectory = file("$rootProject.projectDir/config/checkstyle")
	}
}

group = 'org.fife'
version = '0.1.0-SNAPSHOT'
